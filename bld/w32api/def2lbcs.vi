# Variant of def2lbc for non-x86 architectures
set norc
source ../../getmodn.vi %1
r %1
set nomagic
%s/^LIBRARY.*$//g
%s/^EXPORTS.*$//g
%s/[ \t]//g
#
# process fastcall symbols "@symbol@size"
#
%s/^@\([A-Za-z0-9_]*\)\([@0-9]*\)$/++'__imp_@\1\2'.'%(ModuleName)'.'@\1\2'.'\1'/g
#
# TODO: remove symbols ???? probably C++ ???? they are mangled, should be converted
# to OW mangling
#
%s/^\([A-Za-z0-9_]*\)\([@0-9]*\)=\([A-Za-z0-9_]*\)$//g
#
# process stdcall symbols "symbol@size" - since stdcall essentially does not
# exist on non-x86, simply strip the decoration
#
%s/^\([A-Za-z0-9_]*\)\([@0-9]*\)$/++'__imp_\1'.'%(ModuleName)'.'\1'.'\1'/g
%s/^\+\+'__imp__'.*$//g
%s/\(%(ModuleName)\)/\u\1/g
%s/;.*$//g
w! %2.lbc
q!
